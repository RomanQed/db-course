import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding('UTF-8')
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

group = 'com.github.romanqed.e2e'
archivesBaseName = 'e2e'
version = '1'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':')
    // Jeflect
    implementation group: 'com.github.romanqed', name: 'jeflect', version: '1.1.4L'
    // Annotation processor
    implementation group: 'org.atteo.classindex', name: 'classindex', version: '3.13'
    annotationProcessor group: 'org.atteo.classindex', name: 'classindex', version: '3.13'
    // Logger API
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.13'
    // Javalin
    implementation group: 'io.javalin', name: 'javalin', version: '6.1.4'
    // Postgresql driver
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
    // DI
    implementation group: 'io.github.amayaframework', name: 'amaya-di', version: '2.0.0'
}

shadowJar {
    manifest {
        var date = new Date()
        var javaVersion = System.getProperty("java.version")
        var vmVendor = System.getProperty("java.vm.vendor")
        var vmVersion = System.getProperty("java.vm.version")

        attributes(
                'Main-Class': 'com.github.romanqed.e2e.Main',
                'Created-By': String.format("%s (%s %s)", javaVersion, vmVendor, vmVersion),
                'Gradle-Version': "Gradle $gradle.gradleVersion",
                'Build-Date': new SimpleDateFormat("yyyy-MM-dd").format(date),
                'Build-Time': new SimpleDateFormat("HH:mm:ss.SSSZ").format(date),
                'Built-By': 'Bakaldin Roman',
                'Bundle-Name': 'DB Course E2E',
                'Bundle-Version': archiveVersion,
                'Bundle-SymbolicName': archivesBaseName,
                'Implementation-Title': archivesBaseName,
                'Implementation-Vendor': 'github.com/romanqed',
                'Implementation-Version': archiveVersion,
                'Specification-Title': archivesBaseName,
                'Specification-Vendor': 'github.com/romanqed',
                'Specification-Version': archiveVersion
        )
    }
    archiveBaseName.set(archivesBaseName)
    archiveClassifier.set('')
    archiveVersion.set('')
    mergeServiceFiles('META-INF/annotations')
}
